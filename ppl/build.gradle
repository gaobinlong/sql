/*
 * Copyright OpenSearch Contributors
 * SPDX-License-Identifier: Apache-2.0
 */

/*
 * Licensed to Elasticsearch under one or more contributor
 * license agreements. See the NOTICE file distributed with
 * this work for additional information regarding copyright
 * ownership. Elasticsearch licenses this file to you under
 * the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

plugins {
    id 'java-library'
    id "io.freefair.lombok"
    id 'jacoco'
    id 'antlr'
    id 'com.github.johnrengelman.shadow'
    id 'maven-publish'
}

generateGrammarSource {
    arguments += ['-visitor', '-package', 'org.opensearch.sql.ppl.antlr.parser']
    source = sourceSets.main.antlr
    outputDirectory = file("build/generated-src/antlr/main/org/opensearch/sql/ppl/antlr/parser")
}

configurations {
    compile {
        extendsFrom = extendsFrom.findAll { it != configurations.antlr }
    }
}

dependencies {
    antlr "org.antlr:antlr4:4.7.1"

    runtimeOnly group: 'org.reflections', name: 'reflections', version: '0.9.12'

    implementation "org.antlr:antlr4-runtime:4.7.1"
    implementation group: 'com.google.guava', name: 'guava', version: '32.0.1-jre'
    api group: 'org.json', name: 'json', version: '20230227'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version:'2.20.0'
    api project(':common')
    api project(':core')
    api project(':protocol')
    compileOnly group: 'org.opensearch', name: 'opensearch', version: "${opensearch_version}"
    compileOnly "org.opensearch:common-utils:${common_utils_version}"

    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: '2.1'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.12.4'
    testImplementation(testFixtures(project(":core")))
}

test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

jacocoTestReport {
    reports {
        html.required.set(false)
        xml.required.set(false)
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['**/antlr/parser/**'])
        }))
    }
}
test.finalizedBy(project.tasks.jacocoTestReport)
jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 1.0
            }

        }
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['**/antlr/parser/**'])
        }))
    }
}
check.dependsOn jacocoTestCoverageVerification

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            from components.java

            pom {
                name = "OpenSearch sql ppl"
                packaging = "jar"
                url = "https://github.com/opensearch-project/sql"
                description = "OpenSearch sql ppl"
                scm {
                    connection = "scm:git@github.com:opensearch-project/sql.git"
                    developerConnection = "scm:git@github.com:opensearch-project/sql.git"
                    url = "git@github.com:opensearch-project/sql.git"
                }
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        name = "OpenSearch"
                        url = "https://github.com/opensearch-project/sql"
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = 'staging'
            url = "${rootProject.buildDir}/local-staging-repo"
        }
        maven {
            name = "Snapshots" //  optional target repository name
            url = "https://aws.oss.sonatype.org/content/repositories/snapshots"
            credentials {
                username "$System.env.SONATYPE_USERNAME"
                password "$System.env.SONATYPE_PASSWORD"
            }
        }
    }
}

shadowJar {
    destinationDirectory = file("${project.buildDir}/distributions")
    archiveClassifier.set(null)
    dependencies {
        include(project(':protocol'))
        include(dependency('org.json:json:20230227'))
    }
    exclude 'META-INF/maven/com.google.guava/**'
    exclude 'com/google/thirdparty/**'
    relocate 'com.google.common', 'org.opensearch.ml.repackage.com.google.common'
    relocate 'org.json', 'org.x.json'
}

publishShadowPublicationToMavenLocal.mustRunAfter shadowJar
